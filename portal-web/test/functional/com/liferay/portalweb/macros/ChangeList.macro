definition {

	macro cancelPublicationCreation {
		GlobalMenu.gotoPortlet(
			category = "Change Lists",
			panel = "Applications",
			portlet = "Manage Publications");

		Click(locator1 = "Button#PLUS");

		Type(
			locator1 = "ChangeList#PUBLICATION_NAME",
			value1 = "${publicationName}");

		Button.clickCancel();
	}

	macro createNewPublication {
		if ("${gotoManagePublications}" == "true") {
			GlobalMenu.gotoPortlet(
				category = "Change Lists",
				panel = "Applications",
				portlet = "Manage Publications");

			Click(locator1 = "Button#PLUS");
		}
		else {
			Click(locator1 = "ChangeList#CHANGE_LIST_INDICATOR_BUTTON");

			MenuItem.click(menuItem = "Create New Publication");
		}

		if (isSet(publicationName)) {
			Type(
				locator1 = "ChangeList#PUBLICATION_NAME",
				value1 = "${publicationName}");
		}

		if (isSet(publicationDescription)) {
			Type(
				locator1 = "ChangeList#PUBLICATION_DESCRIPTION",
				value1 = "${publicationDescription}");
		}

		Button.click(button = "Create");

		Pause(locator1 = "5000");
	}

	macro deletePublication {
		Click.waitForMenuToggleJSClick(locator1 = "Icon#BODY_VERTICAL_ELLIPSIS");

		ClickNoError(
			key_menuItem = "Delete",
			locator1 = "MenuItem#ANY_MENU_ITEM");

		AssertConfirm(value1 = "Are you sure you want to delete this publication? You will permanently lose the changes in this publication. You will not be able to undo this operation.");

		AssertTextEquals(
			locator1 = "ChangeList#MANGE_PUBLICATIONS_PAGE_TITLE",
			value1 = "Manage Publications");

		Alert.viewSuccessMessage();
	}

	macro disableChangeList {
		Uncheck.uncheckToggleSwitch(locator1 = "ChangeListSettings#CHANGE_LISTS_TOGGLE_SWITCH");

		Click(locator1 = "Button#SAVE");

		Alert.viewSuccessMessage();
	}

	macro enableChangeList {
		Pause(locator1 = "2000");

		Check.checkToggleSwitch(locator1 = "ChangeListSettings#CHANGE_LISTS_TOGGLE_SWITCH");

		if (isSet(gotoOverview)) {
			Click(locator1 = "ChangeListSettings#SAVE_AND_GO_TO_OVERVIEW_BUTTON");
		}
		else {
			Click(locator1 = "Button#SUBMIT");
		}

		Alert.viewSuccessMessage();
	}

	macro publishPublication {
		Click(locator1 = "ChangeList#CHANGE_LIST_INDICATOR_BUTTON");

		MenuItem.click(menuItem = "Prepare to Publish");

		Click(locator1 = "ChangeList#PUBLISH_BUTTON");

		Alert.viewSuccessMessage();

		var key_navBarTitle = "History";

		AssertTextEquals(
			locator1 = "ChangeList#MANGE_PUBLICATIONS_NAVBAR_TITLE",
			value1 = "History");

		// Workaround LPS-115320

		Refresh();

		var key_publicationName = "${publicationName}";

		AssertTextEquals(
			locator1 = "ChangeList#HISTORY_PUBLICATION_STATUS",
			value1 = "Published");
	}

	macro selectPublication {
		Click(locator1 = "ChangeList#CHANGE_LIST_INDICATOR_BUTTON");

		MenuItem.click(menuItem = "Select a Publication");

		SelectFrame(locator1 = "IFrame#DIALOG");

		var key_publicationName = "${publicationName}";

		Click(
			locator1 = "ChangeList#SELECT_PUBLICATION_NAME",
			value1 = "${publicationName}");

		AssertTextPresent(
			locator1 = "ChangeList#CHANGE_LIST_INDICATOR_TITLE",
			value1 = "${publicationName}");
	}

	macro viewNoStagingMenuitemsNotAvailable {
		AssertElementNotPresent(locator1 = "Icon#HEADER_VERTICAL_ELLIPSIS");

		ProductMenu.gotoPortlet(
			category = "Publishing",
			portlet = "Staging",
			site = "${site}");

		Alert.viewWarningSpecific(warningMessage = "Change lists are enabled for the current virtual instance. Staging and change lists cannot be used together.");
	}

	macro viewPublishedPublicationDetails {
		key_publicationName = "${publicationName}";

		AssertTextEquals(
			locator1 = "ChangeList#PUBLISHED_PUBLICATIONS_NAME",
			value1 = "${publicationName}");

		AssertElementPresent(
			key_publicationPublishedBy = "${publicationPublishedBy}",
			locator1 = "ChangeList#PUBLICATIONS_PUBLISHED_BY");
	}

	macro viewWarningOnPortletExport {
		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			portlet = "${portletName}",
			site = "${site}");

		LAR._gotoExportImport();

		SelectFrame(locator1 = "IFrame#CONFIGURATION_DIALOG");

		Alert.viewWarningSpecific(warningMessage = "Change lists are enabled for the current virtual instance. Export/Import and change lists cannot be used together.");

		IFrame.closeFrame();
	}

	macro viewWarningOnPortletImport {
		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			portlet = "${portletName}",
			site = "${site}");

		LAR._gotoExportImport();

		SelectFrame(locator1 = "IFrame#CONFIGURATION_DIALOG");

		NavItem.click(navItem = "Import");

		Alert.viewWarningSpecific(warningMessage = "Change lists are enabled for the current virtual instance. Export/Import and change lists cannot be used together.");

		IFrame.closeFrame();
	}

	macro viewWarningOnSiteExportImport {
		ProductMenu.gotoPortlet(
			category = "Publishing",
			portlet = "${portletName}",
			site = "${site}");

		Alert.viewWarningSpecific(warningMessage = "Change lists are enabled for the current virtual instance. Export/Import and change lists cannot be used together.");
	}

	macro workOnProduction {
		Click(locator1 = "ChangeList#CHANGE_LIST_INDICATOR_BUTTON");

		MenuItem.click(menuItem = "Work on Production");
	}

}