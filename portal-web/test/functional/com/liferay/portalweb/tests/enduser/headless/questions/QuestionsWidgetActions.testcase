@component-name = "portal-questions"
definition {

	property osgi.module.configuration.file.names = "com.liferay.questions.web.internal.configuration.QuestionsConfiguration-default.config";
	property osgi.module.configurations = "com.liferay.portlet.display-category=&quot;category.collaboration&quot;\nenableRedirectToLogin=&quot;true&quot;";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Questions";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		Navigator.openURL();

		SitePages.openPagesAdmin(siteURLKey = "guest");

		ContentPages.addPage(pageName = "Questions Page");

		PageEditor.addWidget(portletName = "Questions");

		PageEditor.clickPublish();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			ProductMenu.gotoPortlet(
				category = "Content &amp; Data",
				portlet = "Message Boards");

			if (IsElementPresent(locator1 = "Questions#QUESTIONS_LIST")) {
				Questions.tearDown();
			}

			Page.tearDownCP();
		}
	}

	@description = "Validate if the user can not create a question with the maximum of 6 tags with unexisting tag"
	@priority = "2"
	test AddQuestionWithMoreThanTheMaximumTagsIsNotPossible {
		JSONMBMessage.addCategory(
			categoryDescription = "Description",
			categoryName = "Question AddQuestionWithMoreThanTheMaximumTagsIsNotPossible",
			userEmailAddress = "test@liferay.com");

		Navigator.gotoPage(pageName = "Questions Page");

		Click(
			key_questionNameField = "Question AddQuestionWithMoreThanTheMaximumTagsIsNotPossible",
			locator1 = "Questions#QUESTIONS_NAME_FIELD");

		Questions.askQuestionFillingTheMandatoryFields(
			kbArticleContent = "Body AddQuestionWithMoreThanTheMaximumTagsIsNotPossible",
			titleField = "Title AddQuestionWithMoreThanTheMaximumTagsIsNotPossible");

		Questions.addTags(tagLocator = "Tag1");
		Questions.addTags(tagLocator = "Tag2");
		Questions.addTags(tagLocator = "Tag3");
		Questions.addTags(tagLocator = "Tag4");
		Questions.addTags(tagLocator = "Tag5");
		Questions.addTags(tagLocator = "Tag6");

		AssertElementPresent(
			key_errorMessage = "This is an invalid tag.",
			locator1 = "Questions#ASK_QUESTION_ERROR_MESSAGE");
	}

	@description = "Validate if the user can create a question with the maximum of 5 tags with unexisting tag"
	@priority = "3"
	test AddQuestionWithTheMaximumTags {
		JSONMBMessage.addCategory(
			categoryDescription = "Description",
			categoryName = "Question AddQuestionWithTheMaximumTags",
			userEmailAddress = "test@liferay.com");

		Navigator.gotoPage(pageName = "Questions Page");

		Click(
			key_questionNameField = "Question AddQuestionWithTheMaximumTags",
			locator1 = "Questions#QUESTIONS_NAME_FIELD");

		Questions.askQuestionFillingTheMandatoryFields(
			kbArticleContent = "Body AddQuestionWithTheMaximumTags",
			titleField = "Title AddQuestionWithTheMaximumTags");

		Questions.addTags(tagLocator = "Tag1");
		Questions.addTags(tagLocator = "Tag2");
		Questions.addTags(tagLocator = "Tag3");
		Questions.addTags(tagLocator = "Tag4");
		Questions.addTags(tagLocator = "Tag5");

		Click(locator1 = "Questions#ASK_QUESTION_POST_YOUR_QUESTION_BUTTON");

		AssertElementPresent(
			key_titleField = "Title AddQuestionWithTheMaximumTags",
			locator1 = "Questions#ASK_QUESTION_SEARCH_QUESTION");
	}

	@description = "Validate if the user can view the topic selector and category label."
	@priority = "4"
	test AddSubcategoryConfigureParentCategoryDisplay {
		JSONMBMessage.addCategory(
			categoryDescription = "Description",
			categoryName = "Questions Test Category",
			userEmailAddress = "test@liferay.com");

		Questions.addQuestionByAPI(
			bodyField = "Question Body 1",
			categoryName = "Questions Test Category",
			tagField = "test tag",
			titleField = "Question 1");

		Navigator.gotoPage(pageName = "Questions Page");

		Click(
			key_questionNameField = "Questions Test Category",
			locator1 = "Questions#QUESTIONS_NAME_FIELD");

		AssertElementNotPresent(
			key_parentCategoryTitle = "Questions Test Category",
			locator1 = "Questions#QUESTIONS_NAVIGATION_PARENT_CATEGORY_TITLE");

		AssertElementNotPresent(
			key_categoryItem = "Questions Test Category",
			locator1 = "Questions#QUESTIONS_CATEGORY_LABEL_ITEM");

		Questions.gotoDetails(
			questionTabField = "Questions",
			questionTitle = "Question 1");

		AssertElementNotPresent(
			key_categoryItem = "Questions Test Category",
			locator1 = "Questions#QUESTIONS_CATEGORY_LABEL_ITEM");

		Navigator.gotoPage(pageName = "Questions Page");

		Click(
			key_questionNameField = "Questions Test Category",
			locator1 = "Questions#QUESTIONS_NAME_FIELD");

		Click(locator1 = "Questions#ASK_QUESTION_BUTTON");

		AssertElementNotPresent(locator1 = "Questions#QUESTIONS_SELECT_TOPIC");

		Questions.gotoTab(questionTabField = "My Activity");

		AssertVisible(
			key_categoryItem = "Questions Test Category",
			locator1 = "Questions#QUESTIONS_CATEGORY_LABEL_ITEM");

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsCategory.gotoCP(categoryName = "Questions Test Category");

		var messageBoardsCategoryURL = selenium.getLocation();
		var parentCategoryId = StringUtil.extractLast("${messageBoardsCategoryURL}", "mbCategoryId=");

		JSONMBMessage.addCategory(
			categoryDescription = "Subcategory Description",
			categoryName = "Questions Test Subcategory",
			parentCategoryId = "${parentCategoryId}",
			userEmailAddress = "test@liferay.com");

		Navigator.gotoPage(pageName = "Questions Page");

		Click(
			key_questionNameField = "Questions Test Category",
			locator1 = "Questions#QUESTIONS_NAME_FIELD");

		AssertVisible(
			key_parentCategoryTitle = "Questions Test Category",
			locator1 = "Questions#QUESTIONS_NAVIGATION_PARENT_CATEGORY_TITLE");

		AssertVisible(
			key_categoryItem = "Questions Test Category",
			locator1 = "Questions#QUESTIONS_CATEGORY_LABEL_ITEM");

		Questions.gotoDetails(
			questionTabField = "Questions",
			questionTitle = "Question 1");

		AssertVisible(
			key_categoryItem = "Questions Test Category",
			locator1 = "Questions#QUESTIONS_CATEGORY_LABEL_ITEM");

		Navigator.gotoPage(pageName = "Questions Page");

		Click(
			key_questionNameField = "Questions Test Category",
			locator1 = "Questions#QUESTIONS_NAME_FIELD");

		Click(locator1 = "Questions#ASK_QUESTION_BUTTON");

		AssertVisible(locator1 = "Questions#QUESTIONS_SELECT_TOPIC");

		Questions.gotoTab(questionTabField = "My Activity");

		AssertVisible(
			key_categoryItem = "Questions Test Category",
			locator1 = "Questions#QUESTIONS_CATEGORY_LABEL_ITEM");
	}

	@description = "Validate if the anonymous user can ask questions and redirected to login."
	@priority = "4"
	test AskQuestionsThroughAnonymousUser {
		GlobalMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Service Access Policy");

		ServiceAccessPolicy.addServiceAccessPolicy(
			defaultSwitch = "true",
			enabled = "true",
			policyName = "QUESTIONS",
			title = "QUESTIONS");

		var allowedServiceSignature = '''
			com.liferay.expando.kernel.service.ExpandoValueService#getData
			com.liferay.message.boards.service.MBCategoryService#getCategoriesCount
			com.liferay.message.boards.service.MBCategoryService#getCategory
			com.liferay.message.boards.service.MBMessageService#fetchMBMessageByUrlSubject
			com.liferay.message.boards.service.MBMessageService#getMessage
		''';

		ServiceAccessPolicy.editServiceAccessPolicy(
			allowedServiceSignature = "${allowedServiceSignature}",
			policyName = "QUESTIONS");

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsCategory.addCP(categoryName = "Questions Test Category");

		Navigator.gotoPage(pageName = "Questions Page");

		User.logoutPG();

		Navigator.gotoPage(pageName = "Questions Page");

		Click(
			key_questionNameField = "Questions Test Category",
			locator1 = "Questions#QUESTIONS_NAME_FIELD");

		Questions.askQuestionFillingTheMandatoryFields(
			anonymousUser = "true",
			kbArticleContent = "Body Anonymous User Ask questions",
			password = "test",
			titleField = "Title Anonymous User Ask questions",
			userEmailAddress = "test@liferay.com");

		Click(locator1 = "Questions#ASK_QUESTION_POST_YOUR_QUESTION_BUTTON");

		AssertTextEquals(
			key_questionTitle = "Title Anonymous User Ask questions",
			locator1 = "Questions#QUESTIONS_TITLE",
			value1 = "Title Anonymous User Ask questions");
	}

	@description = "Validate if the user can search for the questions."
	@priority = "4"
	test SearchForQuestions {
		JSONMBMessage.addCategory(
			categoryDescription = "Description",
			categoryName = "Questions Test Category",
			userEmailAddress = "test@liferay.com");

		Questions.addQuestionByAPI(
			bodyField = "Content1",
			categoryName = "Questions Test Category",
			tagField = "new",
			titleField = "Question1");

		for (var num : list "2,3,4") {
			Questions.addQuestionWithoutTagByAPI(
				bodyField = "Content${num}",
				categoryName = "Questions Test Category",
				titleField = "Question${num}");
		}

		Navigator.gotoPage(pageName = "Questions Page");

		Click(
			key_questionNameField = "Questions Test Category",
			locator1 = "Questions#QUESTIONS_NAME_FIELD");

		Pause(locator = "3000");

		Questions.search(
			resultsInfo = "4 Results for test",
			searchKey = "Test");

		Questions.viewSearchResults(questionsList = "Question1,Question2,Question3,Question4");

		Questions.search(
			resultsInfo = "1 Results for question4",
			searchKey = "Question4");

		Questions.viewSearchResults(questionsList = "Question4");

		Questions.viewNoSearchResults(questionsList = "Question1,Question2,Question3");

		Questions.search(
			resultsInfo = "1 Results for content2",
			searchKey = "Content2");

		Questions.viewSearchResults(questionsList = "Question2");

		Questions.viewNoSearchResults(questionsList = "Question1,Question3,Question4");

		Questions.search(
			resultsInfo = "1 Results for new",
			searchKey = "New");

		Questions.viewSearchResults(questionsList = "Question1");

		Questions.viewNoSearchResults(questionsList = "Question2,Question3,Question4");

		Questions.clearSearchResults();

		for (var num : list "1,2,3,4") {
			AssertVisible(
				key_questionTitle = "Question${num}",
				locator1 = "Questions#QUESTIONS_TITLE");
		}
	}

	@description = "Validate if the My Activity tab appears"
	@priority = "4"
	test ValidateIfTheMyActivityAppears {
		JSONMBMessage.addCategory(
			categoryDescription = "Description",
			categoryName = "Question ValidateIfTheMyActivityAppears",
			userEmailAddress = "test@liferay.com");

		Navigator.gotoPage(pageName = "Questions Page");

		Click(
			key_questionNameField = "Question ValidateIfTheMyActivityAppears",
			locator1 = "Questions#QUESTIONS_NAME_FIELD");

		AssertElementPresent(
			key_questionTabField = "My Activity",
			locator1 = "Questions#QUESTIONS_LIST_TAB_FIELD");
	}

	@description = "Validate if the pagination will appear and works when we have more than 4 questions."
	@priority = "3"
	test ViewQuestionsPagination {
		JSONMBMessage.addCategory(
			categoryDescription = "Description",
			categoryName = "Questions Test Category",
			userEmailAddress = "test@liferay.com");

		for (var num : list "1,2,3,4,5,6,7,8,9,10") {
			Questions.addQuestionByAPI(
				bodyField = "Question Body ${num}",
				categoryName = "Questions Test Category",
				tagField = "test tag",
				titleField = "Question ${num}");
		}

		Navigator.gotoPage(pageName = "Questions Page");

		Click(
			key_questionNameField = "Questions Test Category",
			locator1 = "Questions#QUESTIONS_NAME_FIELD");

		Pause(locator = "3000");

		Questions.viewPaginationResults(results = "Showing 1 to 10 of 10");

		Questions.changePagination(itemsPerPage = "4 items");

		Questions.viewPaginationResults(results = "Showing 1 to 4 of 10");

		Click(locator1 = "Pagination#NEXT_LINK");

		Questions.viewPaginationResults(results = "Showing 5 to 8 of 10");

		Click(locator1 = "Pagination#NEXT_LINK");

		Questions.viewPaginationResults(results = "Showing 9 to 10 of 10");
	}

	@description = "Validate if the user can select the filter and view the related results."
	@priority = "4"
	test ViewQuestionsWithConfiguredFilter {
		JSONMBMessage.addCategory(
			categoryDescription = "Description",
			categoryName = "Questions Test Category",
			userEmailAddress = "test@liferay.com");

		for (var num : list "1,2") {
			Questions.addQuestionByAPI(
				bodyField = "Question Body ${num}",
				categoryName = "Questions Test Category",
				tagField = "test tag",
				titleField = "Question ${num}");
		}

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsCategory.gotoCP(categoryName = "Questions Test Category");

		MessageboardsThread.gotoCP(threadSubject = "Question 1");

		MessageboardsThread.editCP(
			threadBodyEdit = "Question Body 1 Edit",
			threadSubjectEdit = "Question 1 Edit");

		Navigator.gotoPage(pageName = "Questions Page");

		Click(
			key_questionNameField = "Questions Test Category",
			locator1 = "Questions#QUESTIONS_NAME_FIELD");

		Pause(locator = "3000");

		Questions.configureFilter(filterBy = "Latest Edited");

		Questions.viewFilterResults(
			lastQuestionTitle = "Question 2",
			questionTitle = "Question 1 Edit");

		Questions.configureFilter(filterBy = "Voted in the Last Week");

		for (var num : list "1 Edit,2") {
			AssertElementNotPresent(
				key_questionTitle = "Question ${num}",
				locator1 = "Questions#QUESTIONS_TITLE");
		}

		Questions.configureFilter(filterBy = "Voted in the Last Month");

		for (var num : list "1 Edit,2") {
			AssertElementNotPresent(
				key_questionTitle = "Question ${num}",
				locator1 = "Questions#QUESTIONS_TITLE");
		}

		Questions.configureFilter(filterBy = "Latest Created");

		Questions.viewFilterResults(
			lastQuestionTitle = "Question 1 Edit",
			questionTitle = "Question 2");
	}

}