@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Content Pages";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Content Page Name",
			type = "content");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a test for LPS-101330. A user should be able to add a video fragment and the video should play."
	@priority = "5"
	test AddContentPageWithVideoFragment {
		property portal.acceptance = "false";

		// Assert the Home page is a content page in the default site

		AssertVisible(locator1 = "Icon#EDIT_PENCIL");

		ContentPagesNavigator.openEditContentPage(
			pageName = "Content Page Name",
			siteName = "Test Site Name");

		PageEditor.addFragment(
			collectionName = "Basic Components",
			fragmentName = "Video");

		PageEditor.gotoConfiguration(fragmentName = "Video");

		PageEditor.addVideoFragmentURL(videoLink = "https://www.youtube.com/watch?v=2EPZxIC5ogU");

		PageEditor.clickPublish();

		ContentPagesNavigator.openViewContentPage(
			pageName = "Content Page Name",
			siteName = "Test Site Name");

		YoutubePortlet.assertVideoTitle(videoTitle = "Life at Liferay - A Look into Liferay Culture");
	}

	@description = "This is a use case for LPS-86141. The fragment should be added to all cells of the Collection Display when add it to one cell."
	@priority = "5"
	test AddFragmentToOneCellOfCollectionDisplay {
		property portal.acceptance = "true";

		task ("Add three Web Content Articles based on Basic Web Content") {
			for (var entryTitle : list "First Web Content,Second Web Content,Third Web Content") {
				JSONWebcontent.addWebContent(
					content = "Web Content Content",
					groupName = "Test Site Name",
					title = "${entryTitle}");
			}
		}

		task ("Add a dynamic Collections and select Web Content Article as Asset Entry Type") {
			JSONAssetlist.addDynamicAssetListEntry(
				groupName = "Test Site Name",
				title = "Dynamic Collection");

			AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-site-name");

			AssetListsAdmin.editAssetList(assetListTitle = "Dynamic Collection");

			AssetListsAdmin.configureAssetEntryType(selectedAsset = "Web Content Article");
		}

		task ("Add a Collection Display to page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Content Display",
				fragmentName = "Collection Display");
		}

		task ("Select the dynamic collection") {
			PageEditor.editCollectionDisplay(
				assetListName = "Dynamic Collection",
				fragmentName = "Collection Display");
		}

		task ("Add a Button fragment to the first cell") {
			PageEditor.addFragmentToCollectionDisplay(
				collectionName = "Basic Components",
				entryTitle = "Third Web Content",
				fragmentName = "Button");

			AssertElementPresent(
				key_colPosition = "1",
				key_element = "//div[contains(@class,'page-editor')]//li[contains(.,'Button')]",
				key_fragmentName = "Collection Display",
				key_rowPosition = "1",
				locator1 = "PageEditor#COLLECTION_CELL_NESTED_ELEMENT");
		}

		task ("Assert the Button fragment is also added to other cells") {
			AssertElementPresent(
				key_colPosition = "1",
				key_element = "//div[contains(@class,'page-editor')]//li[contains(.,'Button')]",
				key_fragmentName = "Collection Display",
				key_rowPosition = "2",
				locator1 = "PageEditor#COLLECTION_CELL_NESTED_ELEMENT");

			AssertElementPresent(
				key_colPosition = "1",
				key_element = "//div[contains(@class,'page-editor')]//li[contains(.,'Button')]",
				key_fragmentName = "Collection Display",
				key_rowPosition = "3",
				locator1 = "PageEditor#COLLECTION_CELL_NESTED_ELEMENT");
		}
	}

	@description = "This is a use case for LPS-111274. The selected Tabs fragment should always stay selected when click on the tabs of it."
	@priority = "5"
	test ClickTabsFragmentTabAndViewHeader {
		ContentPagesNavigator.openEditContentPage(
			pageName = "Content Page Name",
			siteName = "Test Site Name");

		PageEditor.addFragment(
			collectionName = "Basic Components",
			fragmentName = "Tabs");

		AssertVisible(
			key_fragmentName = "Tabs",
			locator1 = "Fragment#FRAGMENT_HEADER");

		for (var navItem : list "Tab 2,Tab 3,Tab 4") {
			PageEditor.gotoTabsFragmentNavItem(navItem = "${navItem}");

			AssertVisible(
				key_fragmentName = "Tabs",
				locator1 = "Fragment#FRAGMENT_HEADER");
		}
	}

	@description = "This is a use case for LPS-111274. The light blue frame is shown when hover on unmapped editable field of selected fragments but the dark blue frame is shown when click on unmapped editable field of selected fragments."
	@priority = "5"
	test ClickUnmappedEditableFieldAndMouseOver {
		ContentPagesNavigator.openEditContentPage(
			pageName = "Content Page Name",
			siteName = "Test Site Name");

		PageEditor.addFragment(
			collectionName = "Basic Components",
			fragmentName = "Heading");

		PageEditor.clickEditableField(
			contributed = "true",
			fragmentName = "Heading",
			id = "element-text");

		Pause(locator1 = "1000");

		AssertCssValue(
			id = "element-text",
			key_fragmentName = "Heading",
			locator1 = "Fragment#CONTRIBUTED_FRAGMENT_EDITABLE_FIELD",
			locator2 = "boxShadow",
			value1 = "rgb(11, 95, 255) 0px 0px 0px 1px inset");

		Click(locator1 = "PageEditor#CONTENT_PAGE_DISABLED_HEADER");

		Click.clickNoWaitForVisible(
			key_fragmentName = "Heading",
			locator1 = "Fragment#FRAGMENT_HEADER");

		MouseOver.mouseOverNotVisible(
			id = "element-text",
			key_fragmentName = "Heading",
			locator1 = "Fragment#CONTRIBUTED_FRAGMENT_EDITABLE_FIELD");

		Pause(locator1 = "1000");

		AssertCssValue(
			id = "element-text",
			key_fragmentName = "Heading",
			locator1 = "Fragment#CONTRIBUTED_FRAGMENT_EDITABLE_FIELD",
			locator2 = "boxShadow",
			value1 = "rgb(128, 172, 255) 0px 0px 0px 1px inset");
	}

	@description = "This is a use case for LPS-86141. The collections with multiple asset type are unavailable to the Collection Display."
	@priority = "4"
	test CollectionsWithMultipleAssetTypeAreUnavailableToCollectionDisplay {
		task ("Add a Web Content Article based on Basic Web Content and a Blogs Entry") {
			JSONWebcontent.addWebContent(
				content = "Web Content Content",
				groupName = "Test Site Name",
				title = "Web Content Title");

			JSONBlog.addEntry(
				entryContent = "Blogs Entry Content",
				entryTitle = "Blogs Entry Title",
				groupName = "Test Site Name");
		}

		task ("Add a dynamic Collections and select Any as Asset Entry Type") {
			JSONAssetlist.addDynamicAssetListEntry(
				groupName = "Test Site Name",
				title = "Dynamic Collection");

			AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-site-name");

			AssetListsAdmin.editAssetList(assetListTitle = "Dynamic Collection");

			AssetListsAdmin.configureAssetEntryType(selectedAsset = "Any");
		}

		task ("Add a manual Collections and select the Web Content Article and Blogs entry") {
			JSONAssetlist.addManualAssetListEntry(
				groupName = "Test Site Name",
				title = "Manual Collection");

			AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-site-name");

			AssetListsAdmin.selectAsset(
				assetListName = "Manual Collection",
				assetName = "Web Content Title",
				assetType = "Basic Web Content");

			AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-site-name");

			AssetListsAdmin.selectAsset(
				assetListName = "Manual Collection",
				assetName = "Blogs Entry Title",
				assetType = "Blogs Entry");
		}

		task ("Add a Collection Display to page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Content Display",
				fragmentName = "Collection Display");
		}

		task ("Assert both collections are unavailable to Collection Display") {
			Click.clickNoMouseOver(
				key_elementType = "Collection Display",
				locator1 = "PageEditor#FRAGMENT_BODY_TOOLBAR_CONFIGURATION_BUTTON");

			Click(locator1 = "PageEditor#FRAGMENT_BODY_FLOATING_TOOLBAR_PLUS_BUTTON");

			SelectFrame(locator1 = "IFrame#DIALOG");

			NavItem.click(navItem = "Collections");

			for (var assetListName : list "Dynamic Collection,Manual Collection") {
				LexiconCard.viewCardNotPresent(card = "${assetListName}");
			}
		}
	}

	@description = "This is a use case for for LPS-111548. Reloading the page will clean all Undo states."
	@priority = "4"
	test ConfigureHeadingFragmentAndRefreshPageWithUndoEnabled {
		property osgi.module.configuration.file.names = "com.liferay.layout.content.page.editor.web.internal.configuration.FFLayoutContentPageEditorConfiguration.config";
		property osgi.module.configurations = "undoEnabled = true";

		ContentPagesNavigator.openEditContentPage(
			pageName = "Content Page Name",
			siteName = "Test Site Name");

		PageEditor.addFragment(
			collectionName = "Basic Components",
			fragmentName = "Heading");

		PageEditor.waitForAutoSave();

		AssertVisible(
			key_text = "undo",
			locator1 = "Icon#ANY_ENABLED");

		PageEditor.gotoConfiguration(fragmentName = "Heading");

		PageEditor.configureFragment(textAlign = "Center");

		PageEditor.viewFragmentStyle(
			alignment = "center",
			fragmentName = "heading");

		Refresh();

		AssertElementPresent(
			key_text = "undo",
			locator1 = "Icon#ANY_DISABLED");
	}

	@description = "Given a content page with a configured Content fragment, when the content is deleted the fragment should not be visible in view mode and a message should be shown in page editor."
	@priority = "4"
	test DeletedContentInContentDisplayFragmentIsNotVisible {
		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			groupName = "Test Site Name",
			title = "WebContent Title");

		ContentPagesNavigator.openEditContentPage(
			pageName = "Content Page Name",
			siteName = "Test Site Name");

		PageEditor.addFragment(
			collectionName = "Content Display",
			fragmentName = "Content");

		PageEditor.editContentDisplay(
			fragmentName = "Content",
			webcontent = "true",
			webContentContent = "WC WebContent Content",
			webContentTitle = "WebContent Title");

		PageEditor.clickPublish();

		JSONWebcontent.deleteWebContent(
			groupName = "Test Site Name",
			title = "WebContent Title");

		ContentPagesNavigator.openViewContentPage(
			pageName = "Content Page Name",
			siteName = "Test Site Name");

		task ("Assert the fragment is not visible in the site page") {
			AssertElementNotPresent(
				key_pageName = "Content Page Name",
				locator1 = "PageEditor#CONTENT_PAGE_TEXT_CONTENT");
		}

		ContentPages.clickPencil();

		task ("Assert there is a message in page editor informing the page author that the content is no longer available") {
			AssertVisible(
				key_fragmentName = "Content",
				key_messageIfo = "The selected content is no longer available. Please select another.",
				locator1 = "Fragment#MESSAGE_INFO");
		}
	}

	@description = "This case ensures that a user can map a blogs content field to a fragment via the content display."
	@priority = "5"
	test MapBlogContentViaContentDisplay {
		property testray.component.names = "Content Pages,Item Selector";

		JSONBlog.addEntry(
			entryContent = "This content maps from blog content",
			entryTitle = "Blogs Entry Title",
			groupName = "Test Site Name");

		ContentPagesNavigator.openEditContentPage(
			pageName = "Content Page Name",
			siteName = "Test Site Name");

		PageEditor.addFragment(
			collectionName = "Content Display",
			fragmentName = "Content");

		PageEditor.editContentDisplay(
			blogs = "true",
			entryContent = "This content maps from blog content",
			entryTitle = "Blogs Entry Title",
			fragmentName = "Content");

		PageEditor.clickPublish();

		ContentPagesNavigator.openViewContentPage(
			pageName = "Content Page Name",
			siteName = "Test Site Name");

		AssertTextEquals(
			locator1 = "PageEditor#CONTENT_PAGE_TEXT_CONTENT",
			value1 = "This content maps from blog content");
	}

	@description = "This is a use case for LPS-86141. The mapped content of asset entries in selected collection should be respectively shown in each cell of the Collection Display when map content to editable field of one fragment in one cell."
	@priority = "4"
	test MapContentToEditableFieldOfFragmentInCollectionDisplay {
		task ("Add three Web Content Articles based on Basic Web Content") {
			for (var entryTitle : list "First Web Content,Second Web Content,Third Web Content") {
				JSONWebcontent.addWebContent(
					content = "Web Content Content",
					groupName = "Test Site Name",
					title = "${entryTitle}");
			}
		}

		task ("Add a dynamic Collections and select Web Content Article as Asset Entry Type") {
			JSONAssetlist.addDynamicAssetListEntry(
				groupName = "Test Site Name",
				title = "Dynamic Collection");

			AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-site-name");

			AssetListsAdmin.editAssetList(assetListTitle = "Dynamic Collection");

			AssetListsAdmin.configureAssetEntryType(selectedAsset = "Web Content Article");
		}

		task ("Add a Collection Display to page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Content Display",
				fragmentName = "Collection Display");
		}

		task ("Select the dynamic collection") {
			PageEditor.editCollectionDisplay(
				assetListName = "Dynamic Collection",
				fragmentName = "Collection Display");
		}

		task ("Add a Heading fragment to the first cell") {
			PageEditor.gotoTab(tabName = "Fragments");

			PageEditor.expandFragmentCollection(collectionName = "Basic Components");

			DragAndDrop.javaScriptDragAndDropToObject(
				key_collectionName = "Basic Components",
				key_entryTitle = "Third Web Content",
				key_fragmentName = "Heading",
				locator1 = "PageEditor#FRAGMENTS_AND_WIDGETS_TAB_FRAGMENT",
				locator2 = "PageEditor#COLLECTION_CELL",
				value1 = "");

			PageEditor.waitForAutoSave();
		}

		task ("Map Title of collection items to editable field") {
			Click(locator1 = "PageEditor#CONTENT_PAGE_DISABLED_HEADER");

			Click.clickNoWaitForVisible(
				key_colPosition = "1",
				key_element = "//div[contains(@class,'page-editor')]//li[contains(.,'Heading')]",
				key_fragmentName = "Collection Display",
				key_rowPosition = "1",
				locator1 = "PageEditor#COLLECTION_CELL_NESTED_ELEMENT");

			Click(
				key_colPosition = "1",
				key_element = "//div[contains(@class,'page-editor__topper') and contains(.,'Heading')]//*[contains(@data-lfr-editable-id,'element-text')]",
				key_fragmentName = "Collection Display",
				key_rowPosition = "1",
				locator1 = "PageEditor#COLLECTION_CELL_NESTED_ELEMENT");

			Click(locator1 = "PageEditor#EDITABLE_FIELD_TOOLBAR_MAP_BUTTON");

			Pause(locator1 = "3000");

			Select(
				key_fieldLabel = "Field",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "Title");

			PageEditor.waitForAutoSave();

			AssertSelectedLabel(
				key_fieldLabel = "Field",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "Title");
		}

		task ("Assert the title of three Web Content Articles are respectively shown in each cell") {
			AssertTextEquals(
				key_colPosition = "1",
				key_element = "//div[contains(@class,'page-editor__topper') and contains(.,'Heading')]//*[contains(@data-lfr-editable-id,'element-text')]",
				key_fragmentName = "Collection Display",
				key_rowPosition = "1",
				locator1 = "PageEditor#COLLECTION_CELL_NESTED_ELEMENT",
				value1 = "Third Web Content");

			AssertTextEquals(
				key_colPosition = "1",
				key_element = "//div[contains(@class,'page-editor__topper') and contains(.,'Heading')]//*[contains(@data-lfr-editable-id,'element-text')]",
				key_fragmentName = "Collection Display",
				key_rowPosition = "2",
				locator1 = "PageEditor#COLLECTION_CELL_NESTED_ELEMENT",
				value1 = "Second Web Content");

			AssertTextEquals(
				key_colPosition = "1",
				key_element = "//div[contains(@class,'page-editor__topper') and contains(.,'Heading')]//*[contains(@data-lfr-editable-id,'element-text')]",
				key_fragmentName = "Collection Display",
				key_rowPosition = "3",
				locator1 = "PageEditor#COLLECTION_CELL_NESTED_ELEMENT",
				value1 = "First Web Content");
		}
	}

	@description = "This case ensures that a user can map an image to a fragment via the content display."
	@priority = "5"
	test MapImageViaContentDisplay {
		property portal.upstream = "quarantine";

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Document_1.jpg",
			groupName = "Test Site Name",
			mimeType = "image/jpeg",
			sourceFileName = "Document_1.jpg");

		ContentPagesNavigator.openEditContentPage(
			pageName = "Content Page Name",
			siteName = "Test Site Name");

		PageEditor.addFragment(
			collectionName = "Content Display",
			fragmentName = "Content");

		PageEditor.editContentDisplay(
			documentandmedia = "true",
			fragmentName = "Content",
			imageFileName = "Document_1.jpg");

		PageEditor.clickPublish();

		ContentPagesNavigator.openViewContentPage(
			pageName = "Content Page Name",
			siteName = "Test Site Name");

		AssertVisible(
			key_imageFileName = "Document_1.jpg",
			locator1 = "PageEditor#CONTENT_PAGE_IMAGE_CONTENT");
	}

	@description = "This case ensures that a user can map the content field from web content to a fragment via the content display."
	@priority = "5"
	test MapWebContentContentViaContentDisplay {
		JSONWebcontent.addWebContent(
			content = "This content maps from web content",
			groupName = "Test Site Name",
			title = "Webcontent Title");

		ContentPagesNavigator.openEditContentPage(
			pageName = "Content Page Name",
			siteName = "Test Site Name");

		PageEditor.addFragment(
			collectionName = "Content Display",
			fragmentName = "Content");

		PageEditor.editContentDisplay(
			fragmentName = "Content",
			webcontent = "true",
			webContentContent = "This content maps from web content",
			webContentTitle = "Webcontent Title");

		PageEditor.clickPublish();

		ContentPagesNavigator.openViewContentPage(
			pageName = "Content Page Name",
			siteName = "Test Site Name");

		AssertTextEquals.assertPartialTextAceEditor(
			locator1 = "PageEditor#CONTENT_PAGE_WEB_CONTENT_CONTENT",
			value1 = "This content maps from web content");
	}

	@description = "This is a test for LPS-101249. Can only select Documents and Media when set itemType to FileEntry."
	@priority = "3"
	test OnlySelectDocumentsAndMediaViaFragmentWhenItemTypeIsFileEntry {
		task ("Add a WC based on Basic Web Content") {
			JSONWebcontent.addWebContent(
				content = "Web Content Content",
				description = "Web Content Description",
				groupName = "Test Site Name",
				title = "Web Content Title");
		}

		task ("Add a blog") {
			JSONBlog.addEntry(
				entryContent = "Blogs Entry Content",
				entryTitle = "Blogs Entry Title",
				groupName = "Test Site Name");
		}

		task ("Add a document with an image") {
			JSONDocument.addFileWithUploadedFile(
				dmDocumentDescription = "DM Document Description",
				dmDocumentTitle = "DM Document Title",
				groupName = "Test Site Name",
				mimeType = "image/jpeg",
				sourceFileName = "Document_1.jpg");
		}

		task ("Import test fragments from dependencies folder") {
			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "test-site-name");

			FragmentsAdmin.importCollectionFromDependenciesFolder(collectionZipFile = "display-collection-name.zip");
		}

		task ("Add new fragment to content page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "imported",
				fragmentName = "Documents and Media");
		}

		task ("Open select popup") {
			PageEditor.gotoSelectPanel(fragmentName = "Documents and Media");
		}

		task ("Assert only Documents and Media entry shown") {
			AssertElementPresent(
				key_card = "DM Document Title",
				locator1 = "LexiconCard#CARD");

			AssertElementNotPresent(
				key_navItem = "Web Content",
				locator1 = "NavBar#NAV_ITEM_LINK");

			AssertElementNotPresent(
				key_navItem = "Blogs",
				locator1 = "NavBar#NAV_ITEM_LINK");
		}

		task ("Assert fragment display correctly") {
			LexiconCard.clickCard(card = "DM Document Title");

			AssertTextEquals(
				locator1 = "//div[contains(@class,'fragment_dm')]",
				value1 = "DM Document Title");
		}
	}

	@description = "This is a test for LPS-101249. Can only select web content articles created with a specific structure when set itemSubtype."
	@priority = "3"
	test OnlySelectWebContentBasedOnCustomStructureViaFragmentWhenSetItemSubtype {
		property test.name.skip.portal.instance = "ContentPagesWithContributedFragments#OnlySelectWebContentBasedOnCustomStructureViaFragmentWhenSetItemSubtype";

		task ("Add a WC based on Basic Web Content") {
			JSONWebcontent.addWebContent(
				content = "Web Content Content",
				description = "Web Content Description",
				groupName = "Test Site Name",
				title = "Basic Web Content Title");
		}

		task ("Disable autogenerate structure key") {
			GlobalMenu.gotoPortlet(
				category = "Configuration",
				panel = "Control Panel",
				portlet = "System Settings");

			SystemSettings.gotoConfiguration(
				configurationCategory = "Web Content",
				configurationName = "Administration",
				configurationScope = "System Scope");

			FormFields.disableCheckbox(fieldName = "Autogenerate Structure Key");

			PortletEntry.save();
		}

		task ("Add a structure with Text field") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			NavItem.gotoStructures();

			WebContentStructures.addCP(
				structureDescription = "WC Structure Description",
				structureKey = "STRUCTURE-TEXT",
				structureName = "WC Structure Name");

			DynamicDataMapping.addField(
				field = "Text",
				fieldFieldLabel = "Text",
				fieldName = "Text");

			WebContentStructures.saveCP(structureName = "WC Structure Name");
		}

		task ("Add a template for new structure") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			NavItem.gotoTemplates();

			WebContentTemplates.addCP(
				structureName = "WC Structure Name",
				templateDescription = "WC Template Description",
				templateFieldNames = "Text",
				templateName = "WC Template Name");
		}

		task ("Add a WC based on structure with Text field") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

			WebContent.addWithStructureCP(
				description = "Web Content Description",
				structureName = "WC Structure Name",
				templateName = "WC Template Name",
				webContentText = "Web Content Text",
				webContentTitle = "Web Content With Text Title");

			PortletEntry.publish();

			WebContent.viewTitle(webContentTitle = "Web Content With Text Title");
		}

		task ("Import test fragments from dependencies folder") {
			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "test-site-name");

			FragmentsAdmin.importCollectionFromDependenciesFolder(collectionZipFile = "display-collection-name.zip");
		}

		task ("Add new fragment to content page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "imported",
				fragmentName = "Web Content With Text");
		}

		task ("Open select popup") {
			PageEditor.gotoSelectPanel(fragmentName = "Web Content With Text");
		}

		task ("Assert only WC with text field shown in popup") {
			AssertElementNotPresent(
				key_listEntry = "Basic Web Content Title",
				locator1 = "LexiconList#LIST_ENTRY_TITLE");

			AssertElementPresent(
				key_listEntry = "Web Content With Text Title",
				locator1 = "LexiconList#LIST_ENTRY_TITLE");
		}

		task ("Assert fragment display correctly") {
			AssertClick(
				key_listEntry = "Web Content With Text Title",
				locator1 = "LexiconList#LIST_ENTRY_TITLE",
				value1 = "Web Content With Text Title");

			AssertTextEquals(
				locator1 = "//div[contains(@class,'fragment_wctext')]",
				value1 = "Web Content With Text Title Web Content Description Web Content Text");
		}
	}

	@description = "This is a test for LPS-101249. Can only select web content articles when set itemType to JournalArticle"
	@priority = "5"
	test OnlySelectWebContentViaFragmentWhenItemTypeIsJournalArticle {
		task ("Add a WC based on Basic Web Content") {
			JSONWebcontent.addWebContent(
				content = "Web Content Content",
				description = "Web Content Description",
				groupName = "Test Site Name",
				title = "Web Content Title");
		}

		task ("Add a blog") {
			Navigator.openSiteURL(siteName = "Test Site Name");

			JSONBlog.addEntry(
				entryContent = "Blogs Entry Content",
				entryTitle = "Blogs Entry Title",
				groupName = "Test Site Name");
		}

		task ("Add a document") {
			JSONDocument.addFile(
				dmDocumentDescription = "DM Document Description",
				dmDocumentTitle = "DM Document Title",
				groupName = "Test Site Name");
		}

		task ("Import test fragments from dependencies folder") {
			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "test-site-name");

			FragmentsAdmin.importCollectionFromDependenciesFolder(collectionZipFile = "display-collection-name.zip");
		}

		task ("Add new fragment to content page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "imported",
				fragmentName = "Web Content");
		}

		task ("Open select popup") {
			PageEditor.gotoSelectPanel(fragmentName = "Web Content");
		}

		task ("Assert only Web Content entry shown") {
			AssertElementPresent(
				key_listEntry = "Web Content Title",
				locator1 = "LexiconList#LIST_ENTRY_TITLE");

			AssertElementNotPresent(
				key_navItem = "Documents and Media",
				locator1 = "NavBar#NAV_ITEM_LINK");

			AssertElementNotPresent(
				key_navItem = "Blogs",
				locator1 = "NavBar#NAV_ITEM_LINK");
		}

		task ("Assert fragment display correctly") {
			AssertClick(
				key_listEntry = "Web Content Title",
				locator1 = "LexiconList#LIST_ENTRY_TITLE",
				value1 = "Web Content Title");

			AssertTextEquals(
				locator1 = "//div[contains(@class,'fragment_wc')]",
				value1 = "Web Content Title Web Content Description Web Content Content");
		}
	}

	@description = "This is a use case for LPS-86141. The changes will be propagated to others cells when edit inline content of fragment in one cell."
	@priority = "4"
	test PropagateChangesOfInlineContentToOtherCellsOfCollectionDisplay {
		task ("Add three documents") {
			for (var entryTitle : list "First Document,Second Document,Third Document") {
				JSONDocument.addFile(
					dmDocumentDescription = "DM Description",
					dmDocumentTitle = "${entryTitle}",
					groupName = "Test Site Name");
			}
		}

		task ("Add a dynamic Collections and select Document as Asset Entry Type") {
			JSONAssetlist.addDynamicAssetListEntry(
				groupName = "Test Site Name",
				title = "Dynamic Collection");

			AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-site-name");

			AssetListsAdmin.editAssetList(assetListTitle = "Dynamic Collection");

			AssetListsAdmin.configureAssetEntryType(selectedAsset = "Document");
		}

		task ("Add a Collection Display to page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Content Display",
				fragmentName = "Collection Display");
		}

		task ("Select the dynamic collection and 3 Columns") {
			PageEditor.editCollectionDisplay(
				assetListName = "Dynamic Collection",
				fragmentName = "Collection Display",
				layout = "3 Columns");
		}

		task ("Add a Heading fragment to the first cell") {
			PageEditor.gotoTab(tabName = "Fragments");

			PageEditor.expandFragmentCollection(collectionName = "Basic Components");

			DragAndDrop.javaScriptDragAndDropToObject(
				key_collectionName = "Basic Components",
				key_entryTitle = "Third Document",
				key_fragmentName = "Heading",
				locator1 = "PageEditor#FRAGMENTS_AND_WIDGETS_TAB_FRAGMENT",
				locator2 = "PageEditor#COLLECTION_CELL",
				value1 = "");

			PageEditor.waitForAutoSave();
		}

		task ("Edit inline Content of Heading in the first column") {
			Click(locator1 = "PageEditor#CONTENT_PAGE_DISABLED_HEADER");

			Click.clickNoWaitForVisible(
				key_colPosition = "1",
				key_element = "//div[contains(@class,'page-editor')]//li[contains(.,'Heading')]",
				key_fragmentName = "Collection Display",
				key_rowPosition = "1",
				locator1 = "PageEditor#COLLECTION_CELL_NESTED_ELEMENT");

			Click(
				key_colPosition = "1",
				key_element = "//div[contains(@class,'page-editor__topper') and contains(.,'Heading')]//*[contains(@data-lfr-editable-id,'element-text')]",
				key_fragmentName = "Collection Display",
				key_rowPosition = "1",
				locator1 = "PageEditor#COLLECTION_CELL_NESTED_ELEMENT");

			Click(locator1 = "PageEditor#EDITABLE_FIELD_TOOLBAR_EDIT_BUTTON");

			Type(
				id = "element-text",
				key_fragmentName = "Heading",
				locator1 = "Fragment#CONTRIBUTED_FRAGMENT_EDITABLE_FIELD_CK_EDITOR",
				value1 = "Modified Text");

			PageEditor.waitForAutoSave();

			Click(locator1 = "PageEditor#CONTENT_PAGE_DISABLED_HEADER");

			AssertTextEquals(
				key_colPosition = "1",
				key_element = "//div[contains(@class,'page-editor__topper') and contains(.,'Heading')]//*[contains(@data-lfr-editable-id,'element-text')]",
				key_fragmentName = "Collection Display",
				key_rowPosition = "1",
				locator1 = "PageEditor#COLLECTION_CELL_NESTED_ELEMENT",
				value1 = "Modified Text");
		}

		task ("Assert propagate changes of inline content to other cells") {
			AssertTextEquals(
				key_colPosition = "2",
				key_element = "//div[contains(@class,'page-editor__topper') and contains(.,'Heading')]//*[contains(@data-lfr-editable-id,'element-text')]",
				key_fragmentName = "Collection Display",
				key_rowPosition = "1",
				locator1 = "PageEditor#COLLECTION_CELL_NESTED_ELEMENT",
				value1 = "Modified Text");

			AssertTextEquals(
				key_colPosition = "3",
				key_element = "//div[contains(@class,'page-editor__topper') and contains(.,'Heading')]//*[contains(@data-lfr-editable-id,'element-text')]",
				key_fragmentName = "Collection Display",
				key_rowPosition = "1",
				locator1 = "PageEditor#COLLECTION_CELL_NESTED_ELEMENT",
				value1 = "Modified Text");
		}
	}

	@description = "This is a test for LPS-101248. Can select any custom web content template as a "Template" for web content selected in content fragment."
	@priority = "5"
	test SelectCustomWebContentTemplateViaContentDisplay {
		JSONWebcontent.addWebContent(
			content = "This content maps from web content",
			groupName = "Test Site Name",
			title = "Webcontent Title");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoTemplates();

		WebContentTemplates.addCP(
			generalVariables = "Site ID",
			structureName = "Basic Web Content",
			templateDescription = "WC Template Description",
			templateName = "WC Template Name");

		var siteID = Site.getSiteId();

		ContentPagesNavigator.openEditContentPage(
			pageName = "Content Page Name",
			siteName = "Test Site Name");

		PageEditor.addFragment(
			collectionName = "Content Display",
			fragmentName = "Content");

		PageEditor.editContentDisplay(
			customWebcontenttemplate = "WC Template Name",
			fragmentName = "Content",
			webcontent = "true",
			webContentContent = "${siteID}",
			webContentTitle = "Webcontent Title");

		PageEditor.clickPublish();

		ContentPagesNavigator.openViewContentPage(
			pageName = "Content Page Name",
			siteName = "Test Site Name");

		AssertTextEquals.assertPartialTextAceEditor(
			locator1 = "PageEditor#CONTENT_PAGE_WEB_CONTENT_CONTENT",
			value1 = "${siteID}");

		AssertTextNotEquals.assertNotPartialText(
			locator1 = "PageEditor#CONTENT_PAGE_WEB_CONTENT_CONTENT",
			value1 = "This content maps from web content");
	}

	@description = "This is a use case for LPS-86141. The title of asset entries in selected collection should be shown as a list in the Collection Display."
	@priority = "5"
	test ShowTitleOfCollectionItemsInCollectionDisplay {
		property portal.acceptance = "true";

		task ("Add three Web Content Articles based on Basic Web Content") {
			for (var entryTitle : list "First Web Content,Second Web Content,Third Web Content") {
				JSONWebcontent.addWebContent(
					content = "Web Content Content",
					groupName = "Test Site Name",
					title = "${entryTitle}");
			}
		}

		task ("Add three Blogs entries") {
			for (var entryTitle : list "First Blogs Entry,Second Blogs Entry,Third Blogs Entry") {
				JSONBlog.addEntry(
					entryContent = "Blogs Entry Content",
					entryTitle = "${entryTitle}",
					groupName = "Test Site Name");
			}
		}

		task ("Add a dynamic Collections and select Web Content Article as Asset Entry Type") {
			JSONAssetlist.addDynamicAssetListEntry(
				groupName = "Test Site Name",
				title = "Dynamic Collection");

			AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-site-name");

			AssetListsAdmin.editAssetList(assetListTitle = "Dynamic Collection");

			AssetListsAdmin.configureAssetEntryType(selectedAsset = "Web Content Article");
		}

		task ("Add a manual Collections and select three Blogs entries") {
			JSONAssetlist.addManualAssetListEntry(
				groupName = "Test Site Name",
				title = "Manual Collection");

			AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-site-name");

			AssetListsAdmin.selectAsset(
				assetListName = "Manual Collection",
				assetType = "Blogs Entry",
				entryList = "First Blogs Entry,Second Blogs Entry,Third Blogs Entry");
		}

		task ("Add a Collection Display to page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Content Display",
				fragmentName = "Collection Display");
		}

		task ("Select the dynamic collection") {
			PageEditor.editCollectionDisplay(
				assetListName = "Dynamic Collection",
				fragmentName = "Collection Display");
		}

		task ("Assert the title of three Web Content Articles is shown as a list in Collection Display") {
			AssertVisible(
				key_colPosition = "1",
				key_entryTitle = "Third Web Content",
				key_fragmentName = "Collection Display",
				key_rowPosition = "1",
				locator1 = "PageEditor#COLLECTION_ITEM_TITLE");

			AssertVisible(
				key_colPosition = "1",
				key_entryTitle = "Second Web Content",
				key_fragmentName = "Collection Display",
				key_rowPosition = "2",
				locator1 = "PageEditor#COLLECTION_ITEM_TITLE");

			AssertVisible(
				key_colPosition = "1",
				key_entryTitle = "First Web Content",
				key_fragmentName = "Collection Display",
				key_rowPosition = "3",
				locator1 = "PageEditor#COLLECTION_ITEM_TITLE");
		}

		task ("Select the manual collection") {
			PageEditor.editCollectionDisplay(
				assetListName = "Manual Collection",
				fragmentName = "Collection Display");
		}

		task ("Assert the title of three Web Content Articles is shown as a list in Collection Display") {
			AssertVisible(
				key_colPosition = "1",
				key_entryTitle = "First Blogs Entry",
				key_fragmentName = "Collection Display",
				key_rowPosition = "1",
				locator1 = "PageEditor#COLLECTION_ITEM_TITLE");

			AssertVisible(
				key_colPosition = "1",
				key_entryTitle = "Second Blogs Entry",
				key_fragmentName = "Collection Display",
				key_rowPosition = "2",
				locator1 = "PageEditor#COLLECTION_ITEM_TITLE");

			AssertVisible(
				key_colPosition = "1",
				key_entryTitle = "Third Blogs Entry",
				key_fragmentName = "Collection Display",
				key_rowPosition = "3",
				locator1 = "PageEditor#COLLECTION_ITEM_TITLE");
		}
	}

	@description = "Given a published content page with an unconfigured Content fragment, when the page is viewed the fragment should not be visible."
	@priority = "4"
	test UnconfiguredContentDisplayFragmentIsNotVisible {
		ContentPagesNavigator.openEditContentPage(
			pageName = "Content Page Name",
			siteName = "Test Site Name");

		PageEditor.addFragment(
			collectionName = "Content Display",
			fragmentName = "Content");

		PageEditor.clickPublish();

		ContentPagesNavigator.openViewContentPage(
			pageName = "Content Page Name",
			siteName = "Test Site Name");

		task ("Assert the fragment is not visible in the site page") {
			AssertElementNotPresent(
				key_pageName = "Content Page Name",
				locator1 = "PageEditor#CONTENT_PAGE_TEXT_CONTENT");
		}
	}

	@description = "This is a use case for for LPS-111548. Undo can make the page editor come back to previous state up to 20 last states when the page has more than 20 states."
	@priority = "5"
	test UndoPagechangeTwentyTimes {
		property osgi.module.configuration.file.names = "com.liferay.layout.content.page.editor.web.internal.configuration.FFLayoutContentPageEditorConfiguration.config";
		property osgi.module.configurations = "undoEnabled = true";

		ContentPagesNavigator.openEditContentPage(
			pageName = "Content Page Name",
			siteName = "Test Site Name");

		PageEditor.addFragment(
			collectionName = "Basic Components",
			fragmentName = "Tabs");

		for (var i : list "1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20") {
			PageEditor.gotoConfiguration(fragmentName = "Tabs");

			PageEditor.configureFragment(numberOfTabs = "${i}");

			Click.clickNoMouseOver(
				key_elementType = "Fragment",
				locator1 = "PageEditor#FRAGMENT_BODY_TOOLBAR_CONFIGURATION_BUTTON");
		}

		for (var i : list "1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20") {
			Click(
				key_text = "undo",
				locator1 = "Icon#ANY");
		}

		AssertVisible(
			key_navItem = "Tab 4",
			locator1 = "Fragment#NAV_ITEM_TAB");

		AssertElementNotPresent(
			key_navItem = "Tab 5",
			locator1 = "Fragment#NAV_ITEM_TAB");

		AssertElementPresent(
			key_text = "undo",
			locator1 = "Icon#ANY_DISABLED");
	}

	@description = "This is a test for LPS-112093. The Blogs Entries of collection are displayed using Full Content when add a Content Display to Collection Display."
	@priority = "4"
	test ViewBlogEntryViaContentInCollectionDisplay {
		for (var i : list "1,2,3") {
			JSONBlog.addEntry(
				entryContent = "Blogs Entry Content ${i}",
				entryTitle = "Blogs Entry Title ${i}",
				groupName = "Test Site Name");
		}

		JSONAssetlist.addDynamicAssetListEntry(
			groupName = "Test Site Name",
			title = "Dynamic Asset List Test");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-site-name");

		AssetListsAdmin.editAssetList(assetListTitle = "Dynamic Asset List Test");

		AssetListsAdmin.configureAssetEntryType(selectedAsset = "Blogs Entry");

		ContentPagesNavigator.openEditContentPage(
			pageName = "Content Page Name",
			siteName = "Test Site Name");

		PageEditor.addFragment(
			collectionName = "Content Display",
			fragmentName = "Collection Display");

		PageEditor.editCollectionDisplay(
			assetListName = "Dynamic Asset List Test",
			fragmentName = "Collection Display");

		for (var i : list "1,2,3") {
			if ("${i}" == "1") {
				AssertVisible(
					key_colPosition = "1",
					key_entryTitle = "Blogs Entry Title 3",
					key_fragmentName = "Collection Display",
					key_rowPosition = "${i}",
					locator1 = "PageEditor#COLLECTION_ITEM_TITLE");
			}
			if ("${i}" == "2") {
				AssertVisible(
					key_colPosition = "1",
					key_entryTitle = "Blogs Entry Title ${i}",
					key_fragmentName = "Collection Display",
					key_rowPosition = "${i}",
					locator1 = "PageEditor#COLLECTION_ITEM_TITLE");
			}
			if ("${i}" == "3") {
				AssertVisible(
					key_colPosition = "1",
					key_entryTitle = "Blogs Entry Title 1",
					key_fragmentName = "Collection Display",
					key_rowPosition = "${i}",
					locator1 = "PageEditor#COLLECTION_ITEM_TITLE");
			}
		}

		PageEditor.addFragmentToCollectionDisplay(
			collectionName = "Content Display",
			entryName = "Blogs Entry Title 3",
			fragmentName = "Content");

		for (var i : list "1,2,3") {
			if ("${i}" == "1") {
				AssertVisible(
					key_colPosition = "1",
					key_entryContent = "Blogs Entry Content 3",
					key_fragmentName = "Collection Display",
					key_rowPosition = "${i}",
					locator1 = "PageEditor#COLLECTION_ITEM_CONTENT");
			}
			if ("${i}" == "2") {
				AssertVisible(
					key_colPosition = "1",
					key_entryContent = "Blogs Entry Content ${i}",
					key_fragmentName = "Collection Display",
					key_rowPosition = "${i}",
					locator1 = "PageEditor#COLLECTION_ITEM_CONTENT");
			}
			if ("${i}" == "3") {
				AssertVisible(
					key_colPosition = "1",
					key_entryContent = "Blogs Entry Content 1",
					key_fragmentName = "Collection Display",
					key_rowPosition = "${i}",
					locator1 = "PageEditor#COLLECTION_ITEM_CONTENT");
			}
		}

		PageEditor.waitForAutoSave();

		PageEditor.gotoConfiguration(fragmentName = "Content");

		AssertSelectedLabel(
			key_fieldLabel = "Template",
			locator1 = "Select#GENERIC_SELECT_FIELD",
			value1 = "Full Content");
	}

	@description = "This is a test for LPS-112093. The Web Content Articles of collection are displayed using Default Template when add a Content Display to Collection Display."
	@priority = "5"
	test ViewWebContentViaContentInCollectionDisplay {
		for (var i : list "1,2,3") {
			JSONWebcontent.addWebContent(
				content = "WC WebContent Content ${i}",
				groupName = "Test Site Name",
				title = "WC WebContent Title ${i}");
		}

		JSONAssetlist.addDynamicAssetListEntry(
			groupName = "Test Site Name",
			title = "Dynamic Asset List Test");

		AssetListsAdmin.openAssetListsAdmin(siteURLKey = "test-site-name");

		AssetListsAdmin.editAssetList(assetListTitle = "Dynamic Asset List Test");

		AssetListsAdmin.configureAssetEntryType(selectedAsset = "Web Content Article");

		ContentPagesNavigator.openEditContentPage(
			pageName = "Content Page Name",
			siteName = "Test Site Name");

		PageEditor.addFragment(
			collectionName = "Content Display",
			fragmentName = "Collection Display");

		PageEditor.editCollectionDisplay(
			assetListName = "Dynamic Asset List Test",
			fragmentName = "Collection Display");

		for (var i : list "1,2,3") {
			if ("${i}" == "1") {
				AssertVisible(
					key_colPosition = "1",
					key_entryTitle = "WC WebContent Title 3",
					key_fragmentName = "Collection Display",
					key_rowPosition = "${i}",
					locator1 = "PageEditor#COLLECTION_ITEM_TITLE");
			}
			if ("${i}" == "2") {
				AssertVisible(
					key_colPosition = "1",
					key_entryTitle = "WC WebContent Title ${i}",
					key_fragmentName = "Collection Display",
					key_rowPosition = "${i}",
					locator1 = "PageEditor#COLLECTION_ITEM_TITLE");
			}
			if ("${i}" == "3") {
				AssertVisible(
					key_colPosition = "1",
					key_entryTitle = "WC WebContent Title 1",
					key_fragmentName = "Collection Display",
					key_rowPosition = "${i}",
					locator1 = "PageEditor#COLLECTION_ITEM_TITLE");
			}
		}

		PageEditor.addFragmentToCollectionDisplay(
			collectionName = "Content Display",
			entryName = "WC WebContent Title 3",
			fragmentName = "Content");

		for (var i : list "1,2,3") {
			if ("${i}" == "1") {
				AssertVisible(
					key_colPosition = "1",
					key_entryContent = "WC WebContent Content 3",
					key_fragmentName = "Collection Display",
					key_rowPosition = "${i}",
					locator1 = "PageEditor#COLLECTION_ITEM_CONTENT");
			}
			if ("${i}" == "2") {
				AssertVisible(
					key_colPosition = "1",
					key_entryContent = "WC WebContent Content ${i}",
					key_fragmentName = "Collection Display",
					key_rowPosition = "${i}",
					locator1 = "PageEditor#COLLECTION_ITEM_CONTENT");
			}
			if ("${i}" == "3") {
				AssertVisible(
					key_colPosition = "1",
					key_entryContent = "WC WebContent Content 1",
					key_fragmentName = "Collection Display",
					key_rowPosition = "${i}",
					locator1 = "PageEditor#COLLECTION_ITEM_CONTENT");
			}
		}

		PageEditor.waitForAutoSave();

		PageEditor.gotoConfiguration(fragmentName = "Content");

		AssertSelectedLabel(
			key_fieldLabel = "Template",
			locator1 = "Select#GENERIC_SELECT_FIELD",
			value1 = "Default Template");
	}

}